<Root>

<xsl:stylesheet id="stylesheet" version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:template match="/">
    <html>
        <head>
            <title>SYCL 2020 Conformance Report</title>
            <style type="text/css">
                body {
                    margin: 40px auto;
                    max-width: 800px;
                    font-size: 18px;
                    font-family: Sans-Serif;
                    padding: 0 10px
                }

                h1,h2,h3 {
                    line-height: 1.2
                }

                .site-header {
                    font-weight: bold;
                    text-align: center;
                    line-height: 3;
                }

                table {
                    table-layout: fixed;
                    border-spacing: 0px 2px;
                    width: 100%;
                }

                tr td:nth-child(1) {
                    font-weight: bold;
                    line-height: 1.6;
                }

                tr td:nth-child(2) {
                    text-align: right;
                    line-height: 1.6;
                }

                th {
                    text-align: center;
                    font-weight: bold;
                    line-height: 2;
                    font-size: 18px;
                }

                .test-result {
                    background-color: #ef9a9a;
                }

                .test-result.passed {
                    background-color: #a5d6a7;
                }

                pre {
                    font-family: monospace;
                }

                summary {
                    display: flex;
                }

                details summary::-webkit-details-marker {
                    margin-top: 10px;
                }

            </style>
        </head>
        <body>
            <center>
                <img src="https://upload.wikimedia.org/wikipedia/en/1/19/Khronos_Group_SYCL_logo.svg" width="500px"/>
                <h1>SYCL 2020 Conformance Report</h1>
                <xsl:apply-templates select="Site"/>
                <h2>Test Results</h2>
                <xsl:apply-templates select="Site/Testing/Test"/>
            </center>
        </body>
    </html>
</xsl:template>

<xsl:template match="Site">
    <table>
        <tr><td class="site-header" colspan="2">Implementation</td></tr>
        <tr><td>Name</td><td><xsl:value-of select="./@BuildName" /></td></tr>
        <tr><td class="site-header" colspan="2">Host System</td></tr>
        <tr><td>Date</td><td><xsl:value-of select="/Site/Testing/StartDateTime"/></td></tr>
        <tr><td>Operating System</td><td><xsl:value-of select="./@OSName" /></td></tr>
        <tr><td>OS Release</td><td><xsl:value-of select="./@OSRelease" /></td></tr>
        <tr><td>CPU Architecture</td><td><xsl:value-of select="./@OSPlatform" /></td></tr>
        <tr><td class="site-header" colspan="2">Device Information</td></tr>
        <tr><td>Platform Name</td><td><xsl:value-of select="./@PlatformName" /></td></tr>
        <tr><td>Platform Vendor</td><td><xsl:value-of select="./@PlatformVendor" /></td></tr>
        <tr><td>Platform Version</td><td><xsl:value-of select="./@PlatformVersion" /></td></tr>
        <tr><td>Device Name</td><td><xsl:value-of select="./@DeviceName" /></td></tr>
        <tr><td>Device Vendor</td><td><xsl:value-of select="./@DeviceVendor" /></td></tr>
        <tr><td>Device Version</td><td><xsl:value-of select="./@DeviceVersion" /></td></tr>
        <tr><td class="site-header" colspan="2">Device Extension Support</td></tr>
        <tr><td>Half Precision Floating Point</td><td><xsl:value-of select="./@DeviceFP16" /></td></tr>
        <tr><td>Double Precision Floating Point</td><td><xsl:value-of select="./@DeviceFP64" /></td></tr>
        <tr><td class="site-header" colspan="2">Build Information</td></tr>
        <tr><td>SYCL-CTS commit hash</td><td><xsl:value-of select="./@CommitHash"/></td></tr>
        <tr><td>Full Conformance Mode</td><td><xsl:value-of select="./@FullConformanceMode" /></td></tr>
        <tr><td>CMake Input</td><td><xsl:value-of select="./@CMakeInput" /></td></tr>
        <tr><td>Build System Generator</td><td><xsl:value-of select="./@BuildSystemGenerator" /></td></tr>
        <tr><td>Build System Call</td><td><xsl:value-of select="./@BuildSystemCall" /></td></tr>
        <tr><td>CTest Call</td><td><xsl:value-of select="./@CTestCall" /></td></tr>
    </table>
</xsl:template>

<xsl:template match="Test">
    <details>
        <summary>
            <table>
                <tr class="test-result {@Status}">
                    <td><xsl:value-of select="Name"/></td>
                    <td><xsl:value-of select="./@Status" /></td>
                </tr>
            </table>
        </summary>
        <table>
            <tr>
                <td colspan="2"><pre><xsl:value-of select="Results/Measurement/Value"/></pre></td>
            </tr>
        </table>
    </details>
</xsl:template>

</xsl:stylesheet>

</Root>
